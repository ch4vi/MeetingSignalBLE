import io.gitlab.arturbosch.detekt.Detekt

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        version_name = "1.0.0"
        sdk = [compile: 33, target: 33, min: 30]
        kotlin_version = '1.8.10'
    }
    dependencies {
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.2.0"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.android.application' version '7.4.0' apply false
    id 'com.android.library' version '7.4.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
    id "io.gitlab.arturbosch.detekt" version '1.22.0-RC3'
    id 'com.google.dagger.hilt.android' version '2.45' apply false
}

allprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                lintOptions {
                    checkAllWarnings true
                    warningsAsErrors true
                    htmlReport true
                }
            }
            detekt {
                config = rootProject.files("config/detekt.yml")
                autoCorrect = true
                disableDefaultRuleSets = false
            }
            tasks.withType(Detekt).configureEach {
                reports {
                    html.required.set(true)
                    html.outputLocation.set(file("build/reports/detekt.html"))

                    xml.required.set(false)
                    txt.required.set(false)
                    sarif.required.set(false)
                    md.required.set(false)
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}